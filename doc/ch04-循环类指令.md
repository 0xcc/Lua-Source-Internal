##循环类指令

###相关指令

	OP_FORLOOP,/*   A sBx   R(A)+=R(A+2);if R(A) <?= R(A+1) then { pc+=sBx; R(A+3)=R(A) }*/
	OP_FORPREP,/*   A sBx   R(A)-=R(A+2); pc+=sBx               */
	OP_TFORLOOP,/*  A C R(A+3), ... ,R(A+2+C) := R(A)(R(A+1), R(A+2));if R(A+3) ~= nil then R(A+2)=R(A+3) else pc++   */
	
###数值类循环
涉及数字类循环的指令是OP_FORLOOP和OP_FORPREP,其中OP_FORPREP指令负责循环的初始化操作,它只会在循环开始的时候执行一次,而OP_FORLOOP则负责循环终止条件的判断,它会在每次循环操作开始前执行一次,如果已经不满足循环的执行条件,将会终止循环.

这两个指令需要操作在函数栈上的连续4个数据,同时它们必须是数字类型的数据.其中R(A+1)是循环的结束值,R(A+2)是循环变量每次递增的步长值,R(A)和RA(3)同样都是循环变量,只不过R(A)用于循环变量的初始化和计算的结果,当满足继续循环下去的条件时,再将R(A)赋值给R(A+3),在循环体中实际使用到的循环变量是RA(3).

在循环开始的准备阶段,首先执行OP_FORPREP指令,将循环变量减去步长值,需要注意的是,循环变量最开始的赋值并不在这里执行.然后pc指针将根据sBx参数跳转到OP_FORLOOP指令中,再让循环变量加上步长值,也就是一减一加最后在初始化阶段并没有让循环变量的值发生变化.之所以这样做,是因为OP_FORLOOP指令会在每次进入循环体中首先加上步长值再进行循环条件的判断,为了配合这一操作所以在OP_FORPREP中首先减去了步长值.OP_FORLOOP指令在加上步长值之后会对比循环终止的结束值与当前循环变量的大小,如果仍然可以继续进行循环操作,将把pc指针跳转回循环体的第一段指令,也就是OP_FORPREP的下一条指令,并且将R(A+3)使用R(A)来替换来进行下一次的循环.

这里使用的测试代码是这样的:

	local a = 0; for i = 1, 100, 5 do a = a + i end;

使用ChunkSpy之后反编译的结果如下:

	; source chunk: (interactive mode)
	; x86 standard (32-bit, little endian, doubles)

	; function [0] definition (level 1)
	; 0 upvalues, 0 params, 5 stacks
	.function  0 0 2 5
	.local  "a"  ; 0
	.local  "(for index)"  ; 1
	.local  "(for limit)"  ; 2
	.local  "(for step)"  ; 3
	.local  "i"  ; 4
	.const  0  ; 0
	.const  1  ; 1
	.const  100  ; 2
	.const  5  ; 3
	[1] loadk      0   0        ; 0
	[2] loadk      1   1        ; 1
	[3] loadk      2   2        ; 100
	[4] loadk      3   3        ; 5
	[5] forprep    1   1        ; to [7]
	[6] add        0   0   4
	[7] forloop    1   -2       ; to [6] if loop
	[8] return     0   1
	; end of function

在这里,会创建几个额外的局部变量,可以看到local(1)是循环变量,local(2)是循环的结束值,local(3)是循环的步长,local(4)是循环变量i,在开始循环之前,local(1)到local(3)会依次将它们进行赋值的初始化操作,但是变量i并没有做初始化操作,如前面所说,循环变量的真正的初始化操作是在指令forloop中进行的.

第5行的forprep指令首先将循环变量减去循环步长,再跳转到第7行的forloop操作中,进行循环变量与循环结束值的判断,如果满足继续循环的条件,那么才真正赋值循环变量i为这一次循环的值,同时跳转到第6行的循环体中执行循环的操作.每次执行完循环再次进入forloop指令中判断是否可以终止循环,以此类推.




	




	



 





	
	

	
	




